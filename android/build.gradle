apply plugin: 'com.android.application'

dependencies {
    implementation rootProject
    implementation 'androidx.work:work-runtime:2.11.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'

    implementation 'com.github.tony19:logback-android:3.0.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 35
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1428
        versionName "0.9.$versionCode"
        applicationId "org.bombusmod"
        namespace "$applicationId"
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['key.alias']
            keyPassword keystoreProperties['key.alias.password']
            storeFile keystoreProperties['key.store'] ? file(keystoreProperties['key.store']) : null
            storePassword keystoreProperties['key.store.password']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}
